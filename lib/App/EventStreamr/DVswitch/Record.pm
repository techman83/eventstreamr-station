package App::EventStreamr::DVswitch::Record;
use Method::Signatures;
use Moo;
use namespace::clean;

# ABSTRACT: A DVswitch Standby Process

# VERSION: Generated by DZP::OurPkg:Version

=head1 SYNOPSIS

This Provides a pre-configured DVswitch process.

=head1 DESCRIPTION

This largely extends L<App::EventStreamr::Process>, provides
default cmds that can be overridden in the configuration.

=cut

extends 'App::EventStreamr::Process';

has 'cmd'         => ( is => 'ro', lazy => 1, builder => 1 );
has 'cmd_regex'   => ( is => 'ro', lazy => 1, builder => 1 );
has 'id'          => ( is => 'ro', default => sub { 'dvsink' } );
has 'type'        => ( is => 'ro', default => sub { 'record' } );

method _build_cmd() {
  my $command = $self->{config}{commands}{file} ? $self->{config}{commands}{file} : 'xterm -e "dvsink-files -h $host -p $port $path/%H_%M_%S.dv"';
  
  my %cmd_vars =  (
    host    => $self->{config}{mixer}{host},
    port    => $self->{config}{mixer}{port},
    path    => $self->{status}{$self->{id}}{record_path},
  );

  $command =~ s/\$(\w+)/$cmd_vars{$1}/g;
  return $command;
}

method _build_cmd_regex() {
  return qr:dvsink-files -h $self->{config}{mixer}{host} -p $self->{config}{mixer}{port}.*:;
}


with('App::EventStreamr::Roles::Record');

1;
